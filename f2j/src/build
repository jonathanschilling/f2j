#!/bin/csh -f
#
# This file is part of the Fortran-to-Java (f2java) system,
# developed at the University of Tennessee.
#
# This script helps automate the process of converting fortran
# code containing gotos into runnable java code.  It is assumed
# that the fortran source has already been translated into a
# .java file by f2java.  To translate all the gotos, pass the
# name of the file to this script, e.g.:
#    mymachine> f2java f77code.f
#    mymachine> build F77code.java
# Notice that f2java will convert the first letter of the file to
# uppercase.
# 
# Keith Seymour (seymour@cs.utk.edu)
#

set PNAME = `echo $0 | awk -F/ '{print $NF}'`
set OPT = 

# check the args
#
if($#argv < 1) then
  echo Usage:  $PNAME source_file ...
  exit
endif

foreach file ($argv)

  set JAVA_SRC = $file

  set result = `echo $JAVA_SRC | egrep '.java$'`

  if (! -e $JAVA_SRC) then
    if ("$result" == "") then
      set ALT_JAVA_SRC = $JAVA_SRC.java
      if (! -e $ALT_JAVA_SRC) then
        echo Error\: Can\'t open $JAVA_SRC or $ALT_JAVA_SRC
        continue
      endif
      set JAVA_SRC = $ALT_JAVA_SRC
    else
      echo Error\: Can\'t open $JAVA_SRC
      continue
    endif
  endif

  set CLASS = `echo $JAVA_SRC | sed s/java/class/g`
  set JAS_SRC = `echo $JAVA_SRC | sed s/java/j/g`

  echo === Building $JAVA_SRC
#
# Step 1.  Compile the java .file to a .class file.
#

  echo "javac $OPT $JAVA_SRC"
  rm -f __out
  javac $OPT $JAVA_SRC >& __out

# check for compilation errors
  if(! -z __out) then
   echo "Trouble compiling - check your source file."
   cat __out
   rm -f __out
   continue
  endif
  rm -f __out

#
# Step 2.  Disassemble the class file into a file containing
#          jasmin assembler.
#

  echo "djava -o jasmin $CLASS > $CLASS.out"
  djava -o jasmin $CLASS > $CLASS.out

#
# Step 3.  Translate the 'dummy' gotos (see Dummy.java) to
#          jasmin gotos.  This is done by a perl script called
#          trans, which should be in the current directory.
#

  echo "./trans < $CLASS.out > $JAS_SRC"
  ./trans < $CLASS.out > $JAS_SRC

# Verify that all of the gotos have been translated.

  set result = `egrep Dummy $JAS_SRC`
  if ("$result" != "") then
    echo \*\*
    echo \*\* Error\: not all gotos could be translated\!
    echo \*\*   The resulting code may be unreliable.
    echo \*\*
  endif
  
#
# Step 4.  Assemble the resulting jasmin assembler code into
#          a new class file.
#

  echo "jasmin $JAS_SRC"
  jasmin $JAS_SRC
  rm -f $CLASS.out
  rm -f $JAS_SRC
end
